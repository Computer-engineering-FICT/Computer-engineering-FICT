;                     ЛИСТИНГ 9.1
;    Использование счетчика тактов для определения
;       внутренней тактовой частоты процессора
;
; Автор текста программы Кулаков Владимир Геннадьевич.
; Файл lst09_01.asm, 04.03.2001

IDEAL
P386
LOCALS
MODEL MEDIUM

; Подключить файл мнемонических обозначений
; кодов управляющих клавиш и цветовых кодов
include "lst03_01.inc"
; Подключить файл макросов
include "lst03_04.inc"

SEGMENT sseg para stack 'STACK'
DB 400h DUP(?)
ENDS

DATASEG
; Начальное значение счетчика тактов
StartTSC    DD ?
; Текстовые сообщения
Txt1 DB LIGHTCYAN,0,7,"Определение тактовой частоты "
     DB "процессора при помощи команды RDTSC",0
     DB LIGHTGREEN,12,20
     DB "Тактовая частота процессора (МГц):",0
TxtW DB YELLOW,24,35,"Ждите ...",0
AnyK DB YELLOW,24,29,"Нажмите любую клавишу",0
ENDS

CODESEG
;*****************************
;* Основной модуль программы *
;*****************************
PROC ProcFrequency
        mov     AX,DGROUP
        mov     DS,AX
; Установить текстовый режим и очистить экран
        mov     AX,3
        int     10h
; Скрыть курсор - убрать за нижнюю границу экрана
        mov     [ScreenString],25
        mov     [ScreenColumn],0
        call    SetCursorPosition
; Вывести текстовые сообщения на экран
        MShowColorText 2,Txt1
        MShowColorText 1,TxtW
; Настроить сегментный регистр ES на область данных BIOS
        mov     AX,0
        mov     ES,AX
; Запомнить текущее время
        mov     EAX,[ES:046Ch]
; Ожидать изменения состояния системного таймера
@@T0:   cmp     EAX,[ES:046Ch]
        je      @@T0
; Запомнить значение счетчика тиков
        DB      0Fh,31h        ;команда RDTSC
        mov     [StartTSC],EAX
; Запомнить начальное значение системного времени
        mov     EAX,[ES:046Ch]
; Ожидать в течение 8 тиков
        add     EAX,8
@@T1:   cmp     EAX,[ES:046Ch]
        ja      @@T1
; Получить конечное значение счетчика тиков
        DB      0Fh,31h     ;команда RDTSC
; Вычислить среднюю длительность интервала
; (число тактов в одном тике)
        sub     EAX,[StartTSC]
        shr     EAX,3       ;деление на 8
; Определить тактовую частоту процессора
        ; Умножить длительность интервала на частоту
        ; генератора системного таймера
        mov     EDX,1193180
        mul     EDX
        ; Разделить результат на коэффициент
        ; пересчета системного таймера (65536)
        shrd    EAX,EDX,16
        ; Вычислить частоту в МГц (разделить на 1000000)
        xor     EDX,EDX
        mov     EBX,1000000       
        div     EBX
        ; Вывести результат в десятичном коде
        MShowDecDword 12,55,EAX
; Ожидать нажатия любой клавиши
        MShowColorText 1,AnyK
        call    GetChar
; Переустановить текстовый режим и очистить экран
        mov     AX,3
        int     10h
; Выход в DOS
        mov     AH,4Ch
        int     21h
ENDP ProcFrequency 
ENDS

; Подключить процедуры ввода/вывода
include "lst03_02.inc"
; Подключить процедуры для перевода чисел из двоичного
; кода в десятичный
include "lst03_03.inc"
END
